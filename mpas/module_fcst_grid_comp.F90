#define ESMF_ERR_ABORT(rc) \
if (rc /= ESMF_SUCCESS) write(0,*) 'rc=',rc,__FILE__,__LINE__; if(ESMF_LogFoundError(rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) call ESMF_Finalize(endflag=ESMF_END_ABORT)
! ###########################################################################################
!> \file module_fcst_grid_comp.F90
!>
!> ESMF forecast gridded component for MPAS ATMosphere.
!>
! ###########################################################################################
module module_fcst_grid_comp
  use mpi_f08
  use esmf
  use nuopc
  use atmos_model_mod,    only: atmos_model_init, atmos_model_end, atmos_control_type
  use atmos_model_mod,    only: atmos_model_radiation_physics, atmos_model_dynamics,        &
                                atmos_model_microphysics, update_atmos_model_state
  use module_mpas_config, only: dt_atmos, fcst_mpi_comm, fcst_ntasks, calendar
  use CCPP_data,          only: GFS_control
  use mpas_log,            only : mpas_log_write
  use mpas_derived_types,  only : MPAS_LOG_CRIT

  implicit none
  private

  !---- model defined-types ----
  type(atmos_control_type), save :: Atmos
  integer                        :: n_atmsteps

  !----- coupled model data -----
  integer :: calendar_type = -99
  integer :: date_init(6)

  integer :: mype = 0
  integer, parameter :: THIRTY_DAY_MONTHS = 1,      JULIAN = 2, &
                        GREGORIAN = 3,              NOLEAP = 4, &
                        NO_CALENDAR = 0,  INVALID_CALENDAR =-1

  
  public SetServices

contains

  ! #########################################################################################
  ! ESMF entrypoints for forecast grid-component.
  ! #########################################################################################
  subroutine SetServices(fcst_comp, rc)
    type(ESMF_GridComp)  :: fcst_comp
    integer, intent(out) :: rc

    rc = ESMF_SUCCESS

    ! Initialize
    call ESMF_GridCompSetEntryPoint(fcst_comp, ESMF_METHOD_INITIALIZE, &
                                    userRoutine=fcst_initialize, phase=1, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    ! Advertise
    call ESMF_GridCompSetEntryPoint(fcst_comp, ESMF_METHOD_INITIALIZE, &
                                    userRoutine=fcst_advertise, phase=2, rc=rc)

    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    ! Realize
    call ESMF_GridCompSetEntryPoint(fcst_comp, ESMF_METHOD_INITIALIZE, &
                                    userRoutine=fcst_realize, phase=3, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    ! Run Phase 1
    call ESMF_GridCompSetEntryPoint(fcst_comp, ESMF_METHOD_RUN, &
                                    userRoutine=fcst_run_phase_1, phase=1, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    ! Run Phase 2
    call ESMF_GridCompSetEntryPoint(fcst_comp, ESMF_METHOD_RUN, &
                                    userRoutine=fcst_run_phase_2, phase=2, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    ! Finalize
    call ESMF_GridCompSetEntryPoint(fcst_comp, ESMF_METHOD_FINALIZE, &
                                    userRoutine=fcst_finalize, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return
    
  end subroutine SetServices
  
  ! #########################################################################################
  ! Initialize the ESMF forecast grid component.
  ! #########################################################################################
  subroutine fcst_initialize(fcst_comp, importState, exportState, clock, rc)
    type(esmf_GridComp)                    :: fcst_comp
    type(ESMF_State)                       :: importState, exportState
    type(esmf_Clock)                       :: clock
    integer,intent(out)                    :: rc

    ! Locals
    integer :: i, j, k, n
    type(ESMF_VM) :: VM
    type(ESMF_Time) :: CurrTime, StartTime, StopTime
    type(ESMF_Config) :: cf
    real(kind=8) :: tbeg1
    logical :: fexist
    integer :: io_unit, calendar_type_res, date_res(6), date_init_res(6)
    integer,dimension(6) :: date, date_end, days

    ! Initialize ESMF error message.
    rc = ESMF_SUCCESS
    
    ! Timing info (debug mode)
    tbeg1 = mpi_wtime()
    
    call ESMF_VMGetCurrent(vm=vm,rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    call ESMF_VMGet(vm=vm, localPet=mype, mpiCommunicator=fcst_mpi_comm%mpi_val, &
                    petCount=fcst_ntasks, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return
    if (mype == 0) write(*,*)'in fcst_initialize, fcst_ntasks=',fcst_ntasks

    CF = ESMF_ConfigCreate(rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    ! Load resoure file.
    call ESMF_ConfigLoadFile(config=CF ,filename='model_configure' ,rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    select case( ESMF_UtilStringUpperCase(trim(calendar)) )
    case( 'JULIAN' )
        calendar_type = JULIAN
    case( 'GREGORIAN' )
        calendar_type = GREGORIAN
    case( 'NOLEAP' )
        calendar_type = NOLEAP
    case( 'THIRTY_DAY' )
        calendar_type = THIRTY_DAY_MONTHS
    case( 'NO_CALENDAR' )
        calendar_type = NO_CALENDAR
    case default
        call mpas_log_write( 'fcst_initialize: calendar must be one of '// &
                             'JULIAN|GREGORIAN|NOLEAP|THIRTY_DAY|NO_CALENDAR.',&
                             messageType=MPAS_LOG_CRIT)
    end select

    !call set_calendar_type (calendar_type)

    !
    ! Set atmos time.
    !
    call ESMF_ClockGet(clock, CurrTime=CurrTime, StartTime=StartTime, &
                       StopTime=StopTime, rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

    date_init = 0
    call ESMF_TimeGet (StartTime,                      &
                       YY=date_init(1), MM=date_init(2), DD=date_init(3), &
                       H=date_init(4),  M =date_init(5), S =date_init(6), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return
    if (mype == 0) write(*,'(A,6I5)') 'in fcst_initialize, StartTime=',date_init

    date=0
    call ESMF_TimeGet (CurrTime,                           &
                       YY=date(1), MM=date(2), DD=date(3), &
                       H=date(4),  M =date(5), S =date(6), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return
    if (mype == 0) write(*,'(A,6I5)') 'in fcst_initialize, CurrTime =',date

    date_end=0
    call ESMF_TimeGet (StopTime,                                       &
                       YY=date_end(1), MM=date_end(2), DD=date_end(3), &
                       H=date_end(4),  M =date_end(5), S =date_end(6), rc=rc)
    if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return
    if (mype == 0) write(*,'(A,6I5)') 'in fcst_initialize, StopTime =',date_end

    !
    ! If this is a restarted run ('INPUT/coupler.res' file exists, compare date and date_init
    ! to the values in 'coupler.res'.
    !
    if (mype == 0) then
       inquire(FILE='INPUT/coupler.res', EXIST=fexist)
       if (fexist) then  ! file exists, this is a restart run

          open(newunit=io_unit, file='INPUT/coupler.res', status='old', action='read', err=998)
          read (io_unit,*,err=999) calendar_type_res
          read (io_unit,*) date_init_res
          read (io_unit,*) date_res
          close(io_unit)

          if(date_res(1) == 0 .and. date_init_res(1) /= 0) date_res = date_init_res

          if(mype == 0) write(*,'(A,6(I4))') 'in fcst_initialize, INPUT/coupler.res: date_init=',date_init_res
          if(mype == 0) write(*,'(A,6(I4))') 'in fcst_initialize, INPUT/coupler.res: date     =',date_res

          if (calendar_type /= calendar_type_res) then
             write(0,'(A)')      'fcst_initialize ERROR: calendar_type /= calendar_type_res'
             write(0,'(A,6(I4))')'                       calendar_type     = ', calendar_type
             write(0,'(A,6(I4))')'                       calendar_type_res = ', calendar_type_res
             call ESMF_Finalize(endflag=ESMF_END_ABORT)
          endif

          if (.not. ALL(date_init.EQ.date_init_res)) then
             write(0,'(A)')      'fcst_initialize ERROR: date_init /= date_init_res'
             write(0,'(A,6(I4))')'                       date_init     = ', date_init
             write(0,'(A,6(I4))')'                       date_init_res = ', date_init_res
             call ESMF_Finalize(endflag=ESMF_END_ABORT)
          endif

          if (.not. ALL(date.EQ.date_res)) then
             write(0,'(A)')      'fcst_initialize ERROR: date /= date_res'
             write(0,'(A,6(I4))')'                       date     = ', date
             write(0,'(A,6(I4))')'                       date_res = ', date_res
             call ESMF_Finalize(endflag=ESMF_END_ABORT)
          endif

999       continue
998       continue

       endif ! fexist
    endif ! mype == 0

    if (mype == 0) write(*,*)'fcst_initialize, time_init=', date_init,'time=',date,'time_end=',date_end,'dt_atmos=',dt_atmos

    ! #######################################################################################
    ! Initialize component models.
    ! atmos_model_init() calls the MPAS dycore initialization.
    ! #######################################################################################
    call atmos_model_init(Atmos, fcst_mpi_comm, calendar, CurrTime, StartTime, StopTime)

    ! Timing info (debug mode)
    if (mype == 0) write(*,*)'PASS(fcst_initialize): Time is ', mpi_wtime() - tbeg1
   
  end subroutine fcst_initialize

  ! ###########################################################################################
  ! Advertise the ESMF forecast grid component.
  ! ###########################################################################################
  subroutine fcst_advertise(fcst_comp, importState, exportState, clock, rc)
    type(esmf_GridComp) :: fcst_comp
    type(ESMF_State)    :: importState, exportState
    type(esmf_Clock)    :: clock
    integer,intent(out) :: rc

    ! Initialize ESMF error message.
    rc = ESMF_SUCCESS

  end subroutine fcst_advertise
  
  ! ###########################################################################################
  ! Realize the ESMF forecast grid component.
  ! ###########################################################################################
  subroutine fcst_realize(fcst_comp, importState, exportState, clock, rc)
    type(esmf_GridComp) :: fcst_comp
    type(ESMF_State)    :: importState, exportState
    type(esmf_Clock)    :: clock
    integer,intent(out) :: rc

    ! Initialize ESMF error message.
    rc = ESMF_SUCCESS

  end subroutine fcst_realize
  
  ! ###########################################################################################
  ! Run phase(1) for the ESMF forecast grid component.
  ! ###########################################################################################
  subroutine fcst_run_phase_1(fcst_comp, importState, exportState, clock, rc)
    type(ESMF_GridComp) :: fcst_comp
    type(ESMF_State)    :: importState, exportState
    type(ESMF_Clock)    :: clock
    integer,intent(out) :: rc

    ! Locals
    integer             :: seconds
    real(kind=8)        :: mpi_wtime, tbeg1
    logical,save        :: first=.true.
    integer,save        :: dt_cap=0
    type(ESMF_Time)     :: currTime,stopTime,startTIme
    
    ! Timing info.
    tbeg1 = mpi_wtime()

    ! Initialize ESMF error message.
    rc = ESMF_SUCCESS

    call ESMF_ClockGet(clock, currTime=currTime, startTime=startTime, rc=rc)
    call ESMF_TimeIntervalGet(currTime-StartTime, s=seconds, rc=rc)
    n_atmsteps = seconds/dt_atmos

    if (first) then
       call ESMF_ClockGet(clock, currTime=currTime, stopTime=stopTime, rc=rc)
       if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

       call ESMF_TimeIntervalGet(stopTime-currTime, s=dt_cap, rc=rc)
       if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

       first=.false.
    endif
    
    if ( dt_cap > 0 .and. mod(seconds, dt_cap) == 0 ) then
       Atmos%isAtCapTime = .true.
    else
       Atmos%isAtCapTime = .false.
    endif
    
    ! Call forecast integration subroutines...
    call atmos_model_radiation_physics (Atmos)
    call atmos_model_dynamics (Atmos)
    call atmos_model_microphysics (Atmos)

    ! Timing info (debug mode)
    if (mype == 0) write(*,'(A,I16,A,F16.6)')'PASS(fcstRUN phase 1), n_atmsteps = ', &
                                               n_atmsteps,' time is ',mpi_wtime()-tbeg1
  end subroutine fcst_run_phase_1

  ! ###########################################################################################
  ! Run phase(2) for the ESMF forecast grid component
  ! ###########################################################################################
  subroutine fcst_run_phase_2(fcst_comp, importState, exportState, clock, rc)
    type(ESMF_GridComp) :: fcst_comp
    type(ESMF_State)    :: importState, exportState
    type(ESMF_Clock)    :: clock
    integer,intent(out) :: rc

    real(kind=8)                           :: mpi_wtime, tbeg1
    integer                                :: FBCount, i
    logical                                :: isPresent
    character(len=esmf_maxstr),allocatable :: itemNameList(:)
    type(ESMF_StateItem_Flag), allocatable :: itemTypeList(:)
    type(ESMF_FieldBundle)                 :: fcstExportFB
    ! Timing info.
    tbeg1 = mpi_wtime()

    ! Initialize ESMF error message.
    rc = ESMF_SUCCESS

    call update_atmos_model_state(Atmos)

    ! update fhzero
    call ESMF_StateGet(exportState, itemCount=FBCount, rc=rc)

    allocate (itemNameList(FBCount))
    allocate (itemTypeList(FBCount))
    call ESMF_StateGet(exportState, &
                       itemNameList=itemNameList, &
                       itemTypeList=itemTypeList, &
                       rc=rc)
    do i=1, FBcount
       if (itemTypeList(i) == ESMF_STATEITEM_FIELDBUNDLE) then
          call ESMF_StateGet(exportState, itemName=itemNameList(i), &
                             fieldbundle=fcstExportFB, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

          call ESMF_AttributeGet(fcstExportFB, convention="NetCDF", purpose="MPAS", &
                                 name="fhzero", isPresent=isPresent, rc=rc)
          if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return

          if (isPresent) then
             call ESMF_AttributeSet(fcstExportFB, convention="NetCDF", purpose="FV3", name="fhzero", value=GFS_control%fhzero, rc=rc)
             if (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, line=__LINE__, file=__FILE__)) return
          endif
       else
          !***### anything but a FieldBundle in the state is unexpected here
          call ESMF_LogSetError(ESMF_RC_ARG_BAD,                                 &
                                msg="Only FieldBundles supported in fcstState.", &
                                line=__LINE__, file=__FILE__, rcToReturn=rc)
          return
       endif
    enddo
    
    if (mype == 0) write(*,'(A,I16,A,F16.6)')'PASS: fcstRUN phase 2, n_atmsteps = ', &
                                              n_atmsteps,' time is ',mpi_wtime()-tbeg1

  end subroutine fcst_run_phase_2
  ! ###########################################################################################
  ! Finalize the ESMF forecast grid component.
  ! ###########################################################################################
  subroutine fcst_finalize(fcst_comp, importState, exportState, clock, rc)
    type(esmf_GridComp) :: fcst_comp
    type(ESMF_State)    :: importState, exportState
    type(esmf_Clock)    :: clock
    integer,intent(out) :: rc

    ! Locals
    real(kind=8)        :: mpi_wtime, tbeg1

    ! Initialize ESMF error message.
    rc = ESMF_SUCCESS

     ! Timing info (debug mode)
    tbeg1 = mpi_wtime()
    
    call atmos_model_end (Atmos)

    ! Timing info (debug mode)
    if (mype == 0) write(*,*)'PASS(fcst_finalize): total is ', mpi_wtime() - tbeg1
    
  end subroutine fcst_finalize
end module  module_fcst_grid_comp
